{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 2,
   "source": [
    "#r \"nuget: FParsec\"\n",
    "open FParsec\n",
    "\n",
    "open System\n",
    "open System.Numerics"
   ],
   "outputs": [
    {
     "output_type": "display_data",
     "data": {
      "text/html": [
       "<div><div></div><div></div><div><strong>Installed Packages</strong><ul><li><span>FParsec, 1.1.1</span></li></ul></div></div>"
      ]
     },
     "metadata": {}
    }
   ],
   "metadata": {}
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "source": [
    "type Numbers = BigInt of BigInteger\n",
    "\n",
    "let testBigIntParsing strBigInt =\n",
    "    let toInt (s: string) =\n",
    "        match System.Numerics.BigInteger.TryParse(s) with\n",
    "        | (true, n) -> preturn n\n",
    "        | _ -> fail \"Error parsing Big Int\"\n",
    "\n",
    "    let naturalNum =\n",
    "        many1Chars digit >>= toInt <?> \"natural number\"\n",
    "\n",
    "    match run naturalNum strBigInt with\n",
    "    | Failure (msg, _, _) -> failwith msg\n",
    "    | Success (res, _, _) -> res\n",
    "\n",
    "// this value is bigger than int64 :)\n",
    "let finallyABigInt = \"1844674407370955161510\" |> testBigIntParsing\n",
    "\n",
    "printfn \"Parsed Value: %A; Type: %A\" (finallyABigInt) (finallyABigInt.GetType())"
   ],
   "outputs": [
    {
     "output_type": "stream",
     "name": "stdout",
     "text": [
      "Parsed Value: 1844674407370955161510; Type: System.Numerics.BigInteger\n"
     ]
    }
   ],
   "metadata": {}
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "source": [
    "[<AutoOpen>]\n",
    "module AST =\n",
    "    type Value =\n",
    "        | Int of int\n",
    "        | Str of string\n",
    "        | Bool of bool\n",
    "    \n",
    "    type Operator =\n",
    "        | Add // Arithmetic operators\n",
    "        | Sub\n",
    "        | Mult\n",
    "        | Div\n",
    "        | Mod\n",
    "        | Gt // Comparison operators\n",
    "        | Lt\n",
    "        | Gte\n",
    "        | Lte\n",
    "        | Eq // Equality operators\n",
    "        | Neq\n",
    "        | And // Boolean operators\n",
    "        | Or\n",
    "        | Sconcat // String concatenation\n",
    "    \n",
    "    type Expr =\n",
    "        | Literal of Value\n",
    "        | Variable of name:string\n",
    "        // The Expr type is recursive, as operations\n",
    "        // can consist of expressions\n",
    "        | Operation of (Expr * Operator * Expr)\n",
    "    \n",
    "    type Statement =\n",
    "        | Print of Expr\n",
    "        | Set of name:string * value:Expr\n",
    "        | If of condition:Expr * body:Block * Else:Block option\n",
    "        | While of condition:Expr * body:Block\n",
    "    and Block = Statement list\n",
    "\n",
    "open FParsec\n",
    "\n",
    "[<AutoOpen>]\n",
    "module Parsing =\n",
    "    let pword s = pstring s .>> spaces\n",
    "\n",
    "    // A combinator that transforms a parser by requiring that it\n",
    "    // be wrapped in parentheses\n",
    "    let parens p = between (pword \"(\") (pword \")\") p\n",
    "    \n",
    "    let pbool: Parser<Value, Unit> =\n",
    "        pword \"true\" <|> pword \"false\"\n",
    "        |>> function\n",
    "            | \"true\" -> Bool (true)\n",
    "            | \"false\" -> Bool (false)\n",
    "\n",
    "    // FParsec defines the pint32 parser.\n",
    "    // We simply cast its result to an int\n",
    "    // then construct an Integer Value from it\n",
    "    let pint: Parser<Value, Unit> = pint32 |>> int |>> Int\n",
    "    \n",
    "    let pstringliteral: Parser<Value, Unit> =\n",
    "        // This line returns a list of chars, which we have to\n",
    "        // turn into a string before turning into a Str Value\n",
    "        pchar '\\\"' >>. manyCharsTill anyChar (pchar '\\\"')\n",
    "        |>> string |>> Str\n",
    "        // Discard the spaces at the end\n",
    "        .>> spaces\n",
    "        \n",
    "    let pvalue: Parser<Value, Unit> =\n",
    "        choice [\n",
    "            pint\n",
    "            pstringliteral\n",
    "            pbool\n",
    "        ]\n",
    "        \n",
    "    let test parser strInput =\n",
    "        match run parser strInput with\n",
    "        // Assuming your parser returns something\n",
    "        // that can be printed. For our purposes,\n",
    "        // %O is usually enough. \n",
    "        | Success (result, _, _) -> printfn \"%O\" result\n",
    "        | Failure (error, _, _) -> printfn \"%s\" error\n",
    "\n",
    "    let pliteral: Parser<Expr, Unit> = pvalue |>> Literal\n",
    "\n",
    "    let pidentifier: Parser<string, Unit> =\n",
    "        many1Satisfy2 System.Char.IsLetter System.Char.IsLetterOrDigit\n",
    "        .>> spaces\n",
    "    \n",
    "    let pvariable = pidentifier |>> Variable\n",
    "    \n",
    "    let intOperatorParser =\n",
    "        OperatorPrecedenceParser<Expr, Unit, Unit>()\n",
    "\n",
    "    let intExpr = intOperatorParser.ExpressionParser\n",
    "    \n",
    "    let intTerm = choice [\n",
    "        pint .>> spaces |>> Literal <|> pvariable\n",
    "        parens intExpr\n",
    "    ]\n",
    "    \n",
    "    // Assign the term parser we designed to the\n",
    "    // OperatorPrecedenceParser instance\n",
    "    do intOperatorParser.TermParser <- intTerm\n",
    "    \n",
    "    let createOperation op x y = Operation (x, op, y)\n",
    "\n",
    "    type OperatorDetails = {  Symbol: string;\n",
    "                              Precedence: int;\n",
    "                              Operator: Operator }\n",
    "\n",
    "    let intOperators = [\n",
    "        //{Symbol = \"+\"; Precedence = 2; Operator = Add}\n",
    "        {Symbol = \">\"; Precedence = 1; Operator = Gt}\n",
    "        {Symbol = \"<\"; Precedence = 1; Operator = Lt}\n",
    "        {Symbol = \">=\"; Precedence = 1; Operator = Gte}\n",
    "        {Symbol = \"<=\"; Precedence = 1; Operator = Lte}\n",
    "        {Symbol = \"==\"; Precedence = 1; Operator = Eq}\n",
    "        {Symbol = \"!=\"; Precedence = 1; Operator = Neq}\n",
    "        {Symbol = \"+\"; Precedence = 2; Operator = Add}\n",
    "        {Symbol = \"-\"; Precedence = 2; Operator = Sub}\n",
    "        {Symbol = \"*\"; Precedence = 3; Operator = Mult}\n",
    "        {Symbol = \"/\"; Precedence = 3; Operator = Div}\n",
    "        {Symbol = \"%\"; Precedence = 3; Operator = Mod}\n",
    "    ]\n",
    "\n",
    "    let addOperators (precedenceParser : OperatorPrecedenceParser<_,_,_>) operatorTable =\n",
    "        operatorTable\n",
    "        |> List.iter (fun details ->\n",
    "            let operator =\n",
    "              InfixOperator(\n",
    "                  details.Symbol,\n",
    "                  spaces,\n",
    "                  details.Precedence,\n",
    "                  Associativity.Left,\n",
    "                  createOperation details.Operator\n",
    "              )\n",
    "            precedenceParser.AddOperator(operator))\n",
    "\n",
    "\n",
    "    do addOperators intOperatorParser intOperators\n",
    "    // Define similar structures for booleans and strings\n",
    "    let boolOperatorParser = OperatorPrecedenceParser<Expr, Unit, Unit>()\n",
    "    let boolExpr = boolOperatorParser.ExpressionParser\n",
    "    \n",
    "    let pbool': Parser<bool, Unit> =\n",
    "        pstring \"true\" <|> pstring \"false\"\n",
    "        |>> System.Boolean.Parse\n",
    "    \n",
    "    let pboolval = pbool' |>> Bool\n",
    "\n",
    "    let boolTerm = choice [\n",
    "        pboolval .>> spaces |>> Literal\n",
    "        pvariable\n",
    "        parens boolExpr\n",
    "    ]\n",
    "    \n",
    "    boolOperatorParser.TermParser <- boolTerm\n",
    "\n",
    "    let strOperatorParser = OperatorPrecedenceParser<Expr, Unit, Unit>()\n",
    "    let strExpr = strOperatorParser.ExpressionParser\n",
    "    // We want to make sure we can concatenate\n",
    "    // non-string values with strings, so we\n",
    "    // accept any literal or variable\n",
    "    let strTerm = choice [\n",
    "        pliteral\n",
    "        pvariable\n",
    "        intExpr\n",
    "        boolExpr\n",
    "        parens strExpr \n",
    "    ]\n",
    "    \n",
    "    do strOperatorParser.TermParser <- strTerm\n",
    "    \n",
    "    let boolOperators = [\n",
    "        {Symbol = \"and\"; Precedence = 2; Operator =  And}\n",
    "        {Symbol = \"or\"; Precedence = 1; Operator = Or}\n",
    "    ]\n",
    "\n",
    "    let stringOperators = [\n",
    "        {Symbol = \"++\"; Precedence = 1; Operator = Sconcat}\n",
    "    ]\n",
    "\n",
    "    do addOperators boolOperatorParser boolOperators\n",
    "    do addOperators strOperatorParser stringOperators\n",
    "    \n",
    "    let poperation = choice [\n",
    "        intExpr\n",
    "        boolExpr\n",
    "        strExpr\n",
    "    ]\n",
    "    \n",
    "    let pexpression = choice [\n",
    "        poperation\n",
    "        pliteral\n",
    "        pvariable\n",
    "    ]\n",
    "    \n",
    "    let pstatement, pstatementref =\n",
    "        createParserForwardedToRef<Statement, Unit>()\n",
    "    \n",
    "    let pprint: Parser<Statement, Unit> =\n",
    "        pword \"print\"\n",
    "        >>. parens pexpression\n",
    "        |>> Print\n",
    "        \n",
    "    let pset: Parser<Statement, Unit> =\n",
    "        let identifier =\n",
    "            many1Satisfy2 (System.Char.IsLetter) (System.Char.IsLetterOrDigit)\n",
    "            .>> pword \"=\"\n",
    "\n",
    "        identifier .>>. pexpression\n",
    "        |>> Set\n",
    "\n",
    "    let pblock: Parser<Statement list, Unit> =\n",
    "        between (pword \"{\") (pword \"}\") (many pstatement)\n",
    "    let pif: Parser<Statement, Unit> =\n",
    "        let condition = pword \"if\" >>. pexpression\n",
    "        let inner = pblock\n",
    "        let elseBlock = pword \"else\" >>. pblock |> opt\n",
    "        \n",
    "        pipe3 condition inner elseBlock (fun condition inner elseBlock ->\n",
    "            If (condition, inner, elseBlock))\n",
    "\n",
    "    let pwhile: Parser<Statement, Unit> =\n",
    "        let condition = pword \"while\" >>. pexpression\n",
    "        \n",
    "        condition .>>. pblock\n",
    "        |>> While\n",
    "        \n",
    "    do pstatementref := choice [\n",
    "        pprint\n",
    "        pif\n",
    "        pwhile\n",
    "        pset\n",
    "    ]\n",
    "    \n",
    "    let parseSourceFile fpath =\n",
    "        match runParserOnFile (many pstatement) () fpath System.Text.Encoding.UTF8 with\n",
    "        | Success (result, _, _) -> printfn \"%A\" result\n",
    "        | Failure (error, _, _) -> printfn \"%s\" error\n"
   ],
   "outputs": [
    {
     "output_type": "stream",
     "name": "stderr",
     "text": [
      "\n",
      "input.fsx (50,13)-(50,21) typecheck warning Incomplete pattern matches on this expression. For example, the value '\"a\"' may indicate a case not covered by the pattern(s).\n",
      "\n"
     ]
    }
   ],
   "metadata": {}
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "source": [
    "parseSourceFile \"/home/morph/Desktop/3_oct/PCParser/src/Interpreter/data/sample.txt\""
   ],
   "outputs": [
    {
     "output_type": "stream",
     "name": "stdout",
     "text": [
      "[Set (\"i\", Literal (Int 1));\n",
      " While\n",
      "   (Operation (Variable \"i\", Lte, Literal (Int 100)),\n",
      "    [If\n",
      "       (Operation\n",
      "          (Operation (Variable \"i\", Mod, Literal (Int 15)), Eq, Literal (Int 0)),\n",
      "        [Print (Literal (Str \"fizzbuzz\"))],\n",
      "        Some\n",
      "          [If\n",
      "             (Operation\n",
      "                (Operation (Variable \"i\", Mod, Literal (Int 3)), Eq,\n",
      "                 Literal (Int 0)), [Print (Literal (Str \"fizz\"))],\n",
      "              Some\n",
      "                [If\n",
      "                   (Operation\n",
      "                      (Operation (Variable \"i\", Mod, Literal (Int 5)), Eq,\n",
      "                       Literal (Int 0)), [Print (Literal (Str \"buzz\"))],\n",
      "                    Some [Print (Variable \"i\")])])]);\n",
      "     Set (\"i\", Operation (Variable \"i\", Add, Literal (Int 1)))])]\n"
     ]
    }
   ],
   "metadata": {}
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (F#)",
   "language": "F#",
   "name": ".net-fsharp"
  },
  "language_info": {
   "file_extension": ".fs",
   "mimetype": "text/x-fsharp",
   "name": "F#",
   "pygments_lexer": "fsharp",
   "version": "5.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}